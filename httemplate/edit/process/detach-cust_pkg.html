% if ($error) {
%   $cgi->param('error', $error);
%   $cgi->redirect(popurl(3). 'misc/detach_pkg.html?'. $cgi->query_string );
% } else {

    <% header(emt("Package detached")) %>
      <SCRIPT TYPE="text/javascript">
        window.top.location.reload();
      </SCRIPT>
    </BODY>
    </HTML>

% }
<%init>

my $curuser = $FS::CurrentUser::CurrentUser;

die "access denied"
  unless $curuser->access_right('Change customer package');

my $cust_pkg = qsearchs({
  'table'     => 'cust_pkg',
  'addl_from' => 'LEFT JOIN cust_main USING ( custnum )',
  'hashref'   => { 'pkgnum' => scalar($cgi->param('pkgnum')), },
  'extra_sql' => ' AND '. $curuser->agentnums_sql,
});
die 'unknown pkgnum' unless $cust_pkg;

my $cust_location = new FS::cust_location {
  map { $_ => scalar($cgi->param($_)) } FS::cust_main->location_fields
};

my $cust_main = new FS::cust_main {
  ( map { ( $_, scalar($cgi->param($_)) ) } fields('cust_main') ),
  ( map { ( "ship_$_", '' ) } FS::cust_main->location_fields ),
  'bill_location' => $cust_location,
  'ship_location' => $cust_location,
};

my $pkg_or_error = $cust_pkg->change( {
  'keep_dates' => 1,
  'cust_main'  => $cust_main,
} );

my $error = ref($pkg_or_error) ? '' : $pkg_or_error;

</%init>
