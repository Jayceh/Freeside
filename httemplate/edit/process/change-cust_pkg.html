% if ($error) {
%   $cgi->param('error', $error);
%   $cgi->redirect(popurl(3). 'misc/change_pkg.cgi?'. $cgi->query_string );
% } else {

    <% header(emt("Package changed")) %>
      <SCRIPT TYPE="text/javascript">
        window.top.location.reload();
      </SCRIPT>
    </BODY>
    </HTML>

% }
<%init>

my $curuser = $FS::CurrentUser::CurrentUser;

die "access denied"
  unless $curuser->access_right('Change customer package');

my $cust_pkg = qsearchs({
  'table'     => 'cust_pkg',
  'addl_from' => 'LEFT JOIN cust_main USING ( custnum )',
  'hashref'   => { 'pkgnum' => scalar($cgi->param('pkgnum')), },
  'extra_sql' => ' AND '. $curuser->agentnums_sql,
});
die 'unknown pkgnum' unless $cust_pkg;

my %change = map { $_ => scalar($cgi->param($_)) }
                 qw( locationnum pkgpart );

$change{'keep_dates'} = 1;

if ( $cgi->param('locationnum') == -1 ) {
  my $cust_location = FS::cust_location->new({
    'custnum' => $cust_pkg->custnum,
    map { $_ => scalar($cgi->param($_)) }
        qw( address1 address2 city county state zip country )
  });
  $change{'cust_location'} = $cust_location;
}

my $error;
if ( $cgi->param('delay') ) {
  my $date = parse_datetime($cgi->param('start_date'));
  if (!$date) {
    $error = "Invalid change date '".$cgi->param('start_date')."'.";
  } elsif ( $date < time ) {
    $error = "Change date ".$cgi->param('start_date')." is in the past.";
  } else {
    # schedule the change
    $change{'start_date'} = $date;
    $error = $cust_pkg->change_later(\%change);
  }
} else {
  # special case: if there's a package change scheduled, and it matches
  # the parameters the user requested this time, then change to the existing
  # future package.
  if ( $cust_pkg->change_to_pkgnum ) {
    my $change_to = FS::cust_pkg->by_key($cust_pkg->change_to_pkgnum);
    if ( $change_to->pkgpart      == $change{'pkgpart'} and
         $change_to->locationnum  == $change{'locationnum'} ) {

      %change = ( 'cust_pkg' => $change_to );

    }
  }
    
  # do a package change right now
  my $pkg_or_error = $cust_pkg->change( \%change );
  $error = ref($pkg_or_error) ? '' : $pkg_or_error;
}

</%init>
